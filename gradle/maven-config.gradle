apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'com.caseykulm.retroravelry'
version '0.4.0-SNAPSHOT'

task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
  from javadoc
  archiveClassifier = 'javadoc'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'retroravelry'
      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom {
        name = 'Retro Ravelry'
        description = 'Kotlin library for using the Ravelry API via OkHttp'
        url = 'https://github.com/caseykulm/retroravelry'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            name = 'Casey Kulm'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/caseykulm/retroravelry.git'
          developerConnection = 'scm:git:git@github.com:caseykulm/retroravelry.git'
          url = 'https://github.com/caseykulm/retroravelry'
        }
      }
    }
  }

  repositories {
    maven {
      // change URLs to point to your repos, e.g. http://my.org/repo
      def releasesRepoUrl = "$buildDir/repos/releases"
      def snapshotsRepoUrl = "$buildDir/repos/snapshots"
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}